# deps.cmake - fetch, setup, and link to dependencies
include(FetchContent)

## GLEW (caused by pbo optimization changes)
if(WIN32)
	message(STATUS "glew: windows detected: fetching glew binaries")
	FetchContent_Declare(glew URL https://github.com/nigels-com/glew/releases/download/glew-2.3.0/glew-2.3.0-win32.zip)
	FetchContent_MakeAvailable(glew)
	target_include_directories(audioviz PUBLIC ${glew_SOURCE_DIR}/include)
	if(CMAKE_SYSTEM_PROCESSOR MATCHES "x86_64|AMD64")
		target_link_directories(audioviz PUBLIC ${glew_SOURCE_DIR}/lib/Release/x64)
	else()
		message(FATAL_ERROR "glew: windows other architecture: not implemented, do more work")
	endif()
	target_link_libraries(audioviz PUBLIC glew32s)
	target_compile_definitions(audioviz PUBLIC GLEW_STATIC)
else()
	find_package(GLEW REQUIRED)
	target_link_libraries(audioviz PUBLIC GLEW::GLEW)
endif()

## OpenGL (also caused by optimization changes)
find_package(OpenGL COMPONENTS OpenGL REQUIRED)
target_link_libraries(audioviz PUBLIC OpenGL::GL)

## fftw
if(NOT ANDROID)
	# termux's fftw package is missing FFTW3LibraryDepends.cmake, so don't bother finding
	find_package(FFTW3 COMPONENTS fftw3f QUIET)
endif()
if(WIN32 AND NOT FFTW3_FOUND)
	if(CMAKE_SYSTEM_PROCESSOR MATCHES "x86_64|AMD64")
		message(STATUS "fftw: windows x64 detected: fetching binaries")
		FetchContent_Declare(fftw URL https://fftw.org/pub/fftw/fftw-3.3.5-dll64.zip)
		FetchContent_MakeAvailable(fftw)
		target_link_directories(audioviz PUBLIC ${fftw_SOURCE_DIR})
		target_include_directories(audioviz PUBLIC ${fftw_SOURCE_DIR})
		target_link_libraries(audioviz PUBLIC fftw3f-3)
	else()
		message(STATUS "fftw: windows other architecture (probably ARM64) detected: fetching source")
		set(BUILD_TESTS OFF)
		set(ENABLE_FLOAT ON)
		set(DISABLE_FORTRAN ON)
		FetchContent_Declare(fftw URL https://www.fftw.org/fftw-3.3.10.tar.gz)
		FetchContent_MakeAvailable(fftw)
		target_include_directories(audioviz PUBLIC ${fftw_SOURCE_DIR}/api)
		target_link_libraries(audioviz PUBLIC fftw3f)
	endif()
else()
	target_link_libraries(audioviz PUBLIC fftw3f)
endif()

## sfml (static, without audio and network libs)
set(SFML_STATIC_LIBRARIES ON CACHE BOOL "")
find_package(SFML COMPONENTS Graphics Window System QUIET)
if(NOT SFML_FOUND)
	message(STATUS "sfml: FIND_SFML_ERROR: ${FIND_SFML_ERROR}")
	if(WIN32)
		message(STATUS "sfml: windows detected: fetching binaries")
		FetchContent_Declare(sfml URL https://github.com/SFML/SFML/releases/download/3.0.2/SFML-3.0.2-Windows.MinGW.x64.zip)
		FetchContent_MakeAvailable(sfml)
		list(APPEND CMAKE_PREFIX_PATH ${sfml_SOURCE_DIR}/lib/cmake/SFML)
	else()
		message(STATUS "sfml: fetching source")
		set(SFML_BUILD_AUDIO OFF)
		set(SFML_BUILD_NETWORK OFF)
		FetchContent_Declare(sfml URL https://github.com/SFML/SFML/archive/3.0.2.tar.gz)
		FetchContent_MakeAvailable(sfml)
	endif()
endif()
find_package(SFML COMPONENTS Graphics Window System REQUIRED)
target_link_libraries(audioviz PUBLIC SFML::Graphics)

## spline
if(NOT EXISTS ${CMAKE_CURRENT_BINARY_DIR}/tk-spline.hpp)
	file(DOWNLOAD https://github.com/ttk592/spline/raw/master/src/spline.h ${CMAKE_CURRENT_BINARY_DIR}/tk-spline.hpp)
endif()

## nlohmann_json
FetchContent_Declare(json URL https://github.com/nlohmann/json/releases/download/v3.12.0/json.tar.xz)
FetchContent_MakeAvailable(json)
target_link_libraries(audioviz PUBLIC nlohmann_json::nlohmann_json)

### TEMPORARY - libaudioviz should not be responsible for audio playback.
### but to keep things stable i will leave this as is for now.
## portaudio (optional)
if(AUDIOVIZ_USE_PORTAUDIO)
    FetchContent_Declare(portaudio-pp URL https://github.com/trustytrojan/portaudio-pp/archive/main.tar.gz)
    FetchContent_MakeAvailable(portaudio-pp)
    target_compile_definitions(audioviz PUBLIC AUDIOVIZ_PORTAUDIO)
    target_link_libraries(audioviz PUBLIC portaudio-pp::portaudio-pp)
endif()
